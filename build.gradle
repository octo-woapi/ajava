plugins {
    id 'com.diffplug.spotless' version '6.18.0'
    id 'org.springframework.boot' version "${globalSpringBootVersion}"
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'java'
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
    postgresqlVersion = '42.5.4'
    springBootVersion = "${globalSpringBootVersion}"
    springDocVersion = '2.0.2'
    springSecurityVersion = '6.0.2'
    testcontainersVersion = '1.17.6'
}

group = 'com.octo.ajava'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

    implementation "org.springframework.security:spring-security-config:${springSecurityVersion}"
    implementation "org.springframework.security:spring-security-oauth2-client:${springSecurityVersion}"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"

    //runtimeOnly 'org.postgresql:postgresql'
    implementation "org.postgresql:postgresql:${postgresqlVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "org.springframework.security:spring-security-test:${springSecurityVersion}"
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    //testImplementation 'com.h2database:h2:2.1.214'
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:2.35.0"
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${globalSpringBootVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

spotless {
    java {
        targetExclude "build/**"
        googleJavaFormat()
        removeUnusedImports()
    }
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

wrapper {
    gradleVersion = '7.6'
}
