plugins {
    id 'com.diffplug.spotless' version '7.0.2'
    id 'org.springframework.boot' version "${globalSpringBootVersion}"
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
    id 'java'
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
    junitVersion = '5.11.4'
    restAssuredVersion = '5.5.1'
    restdocsVersion = '3.0.3'
    postgresqlVersion = '42.7.5'
    springBootVersion = "${globalSpringBootVersion}"
    springDocVersion = '2.8.5'
    springSecurityVersion = '6.4.3'
    testcontainersVersion = '1.20.5'
    wiremockVersion = '3.12.0'
}

group = 'com.octo.ajava'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"

    implementation "org.springframework.security:spring-security-config:${springSecurityVersion}"
    implementation "org.springframework.security:spring-security-oauth2-client:${springSecurityVersion}"

    implementation "org.postgresql:postgresql:${postgresqlVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${restdocsVersion}"
    testImplementation "org.springframework.security:spring-security-test:${springSecurityVersion}"
    testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.wiremock:wiremock-standalone:${wiremockVersion}"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${globalSpringBootVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

spotless {
    java {
        targetExclude "build/**"
        googleJavaFormat()
        removeUnusedImports()
    }
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

wrapper {
    gradleVersion = '8.12.1'
}
