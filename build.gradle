import org.codehaus.groovy.runtime.GStringImpl

plugins {
    id 'com.diffplug.spotless' version '7.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
    id 'org.springframework.boot' version "${globalSpringBootVersion}"
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
    log4jVersion = '2.20.0'
    mockitoVersion = '5.2.0'
    postgresqlVersion = '42.7.5'
    springBootVersion = "${globalSpringBootVersion}" as GStringImpl
    springDocVersion = '2.8.5'
    springSecurityVersion = '6.4.2'
    testcontainersVersion = '1.20.4'
}

group = 'com.octo.ajava'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'me.paulschwarz:spring-dotenv:3.0.0'

    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-to-slf4j:${log4jVersion}"

    implementation "org.postgresql:postgresql:${postgresqlVersion}"

    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"

    implementation "org.springframework.security:spring-security-config:${springSecurityVersion}"
    implementation "org.springframework.security:spring-security-oauth2-client:${springSecurityVersion}"
    implementation "org.springframework.security:spring-security-web:${springSecurityVersion}"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"

    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:3.0.1"
    testImplementation "io.rest-assured:rest-assured:5.5.1"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "org.springframework.security:spring-security-test:${springSecurityVersion}"
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${globalSpringBootVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

spotless {
    java {
        targetExclude "build/**"
        googleJavaFormat()
        removeUnusedImports()
    }
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

wrapper {
    gradleVersion = '8.12.1'
}
